<languageVersion : 1.0;>

kernel ScanLines
<   namespace : "mesh";
    vendor : "Mike Chambers";
    version : 1;
    description : "Generates a TV Scanline effect";
>
{
    input image4 src;
    parameter float alpha
    <
    	defaultValue : 0.5;
    	minValue : 0.0;
    	maxValue : 1.0;
    	description : "A coef for lines.";
    >;
    parameter float offset
	<
		defaultValue : 0.1;
		minValue : 0.0;
		maxValue : 1.0;
		description : "A offset for lines.";
	>;
    output pixel4 dst;

    void
    evaluatePixel()
    {
        //get the current pixel
        dst = sampleNearest(src, outCoord());

        //find out the vertical pixel size. In Flash this will always be 1
        float pixelVSize = pixelSize(src).y;

        //pixelVSize / 2.0 - outCoord() gives center of pixel, so we have to adjust to find
        //center of our pixel
        if( int( mod(((pixelVSize * outCoord().y) - pixelVSize / 2.0), 2.0 ) ) != 0)
        {
            //if on an even row, set the pixel to blackt
            dst.r *= alpha;
            dst.r += offset;

            dst.b *= alpha;
            dst.b += offset;

            dst.g *= alpha;
            dst.g += offset;

            //can also do
            ////dst = float4(0.0,0.0,0.0, 1.0);
        }
    }
}

